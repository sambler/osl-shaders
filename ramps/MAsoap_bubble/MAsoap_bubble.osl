/*
    soap bubble shader by Michel J. Anders (c)2012
    from https://github.com/sambler/osl-shaders
    
    license: cc-by-sa
    
    original script from -
    http://blenderthings.blogspot.co.uk/2012/11/a-soap-bubble-osl-shader-for-blender.html
*/

#include "stdosl.h"

surface MAsoapbubble (
        float InteriorIOR  = 1, // approximate refractive index of air
        float SurfaceIOR   = 1.3, // approximate refractive index of water
        float Thickness    = 1000, // 1000 nm = 1 micron
        output color Color = 0)
{
    // condition for constructive interference:
    // 2 * SurfaceIOR * Thickness * cos(t2) == (m-0.5)*lambda
    // Thickness and lambda in nm
    float eta = InteriorIOR/SurfaceIOR;
        // note that N should be the perturbed normal
    vector T = normalize(refract(I,N,eta));
        // no need to divide by (len(-I) * len(T)) as these are normalized
    float cost2 = dot(-I , T);
    float opd = 2*SurfaceIOR*Thickness*cost2;
    
    int mmin = int(ceil(opd/750+0.5));
    int mmax = int(floor(opd/350+0.5));
        // if mmax < mmin the film is too thin to show an effect
    int m = (mmin + mmax)/2;
    
    if (m > 0){
     float lambda = opd / (m - 0.5);
            color c = wavelength_color(lambda);
            Color =  c;
    }
}

